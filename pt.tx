
/*
 Ticket DSL grammar for portuguese ICG tickets without design.
 */


Receipt:
    header = ReceiptHeader
	detail = ReceiptDetail
	taxes = ReceiptTaxes
	means = ReceiptMeans
	footer = Footer
;

/* ===================== HEADER ===================== */

ReceiptHeader:
    company_info = CompanyInfo
    document_name = /.{1,40}/
    document_type = 'Simplificada'?
    receipt_info = ReceiptInfo
;

CompanyInfo:
    commercial_name = /.{1,20}/
    name = /.{1,14}/
    market = /.{1,31}/
    activity = /.{1,40}/
    brand = /.{1,20}/
    phone_number = CompanyPhoneNumber
    fiscal_data = FiscalData
    commercial_registry = CommercialRegistry
    address = CompanyAddress
;

CompanyName:
    value = /.{1,20}/
;

CommercialRegistry:
    'Cons. Reg. Com. Lisboa n.' number = IntegerRegEx
;

CompanyAddress:
    'Sede:' address = /.{1,20}/
    zip_code = /.{1,8}/
    city = /.{1,6}/
;

FiscalData:
    'NIF:' tax_id = /.{1,9}/
    'Cap. Social:' share_capital = IntegerRegEx
    Skip
;

CompanyPhoneNumber:
    'Tel.:' number = IntegerRegEx
;

ReceiptInfo:
    receipt_id = ReceiptId
    cashier_ref = CashierRef
    commercial_name = /.{1,40}/
    receipt_date = ReceiptDate
    Skip
    SeparatorLineRegEx
;

ReceiptId:
    'Doc. Num.:'
    header_serial_number = /\w{1,4}/ '/'
    manager_serial_number = /\w{1,3}/ '/'
    document_number = /\w{1,8}/
;

CashierRef:
    'Vend:'
    reference = IntegerRegEx
;

ReceiptDate:
    'Data:'
    date = DateRegEx
    time = TimeRegEx
    'ORIGINAL' | Skip
;

/* ===================== DETAIL ===================== */

ReceiptDetail:
    receipt_items += ReceiptItem
    SeparatorLineRegEx
    discounts *= Discount
;

ReceiptItem:
    Item | Menu
;

Item:
   quantity = IntegerRegEx
   description = /.{1,18}\s/
   tax = PercentageRegEx
   unit_price = NumberRegEx
   line_discount = LineDiscount?
;

Menu:
   quantity = IntegerRegEx
   description = /[^%]{1,35}\s/
   tax = PercentageRegEx
;

LineDiscount:
    '- Desconto:'
    percentage = PercentageRegEx
    amount = NumberRegEx
;

Discount:
    'ID Desconto:'
    discount_id = /.{1,8}/
    'DTO'
    amount = NumberRegEx
;

/* ===================== TAXES ===================== */

ReceiptTaxes:
    taxes_header = Skip
    taxes *= Tax
    total = TotalWithTax
;

Tax:
    tax_base = NumberRegEx
    percentage = PercentageRegEx
    amount = NumberRegEx
;

TotalWithTax:
    'TOTAL:'
    amount = NumberRegEx
;
/* ===================== MEANS ===================== */

ReceiptMeans:
    payment = Payment
;

Payment:
    means += MeansOfPayment
    troco += Troco
;

MeansOfPayment:
    description = MeansRegEx
    given_amount = NumberRegEx
;

Troco:
    'Troco:' exchange = NumberRegEx
;

/* ===================== FOOTER ===================== */

Footer:
    Skip /* Doc. Liquidado no momento da emissao */
    CancellationDocument?
    customer_info = CustomerInfo
    refund = Refund?
    signature = CheckSignature
    commands *= EscPosCommand
;

CancellationDocument:
    'DOCUMENTO PARA ANULAR' Skip
;

CustomerInfo:
    name = CustomerName?
    'NIF:' tax_id = /.{1,12}/
    address = CustomerAddress?
    city = CustomerCity?
;

CustomerName:
    'Nome:' value = /.{1,34}/?
;

CustomerAddress:
    'Mor.:' value = /.{1,34}/
;

CustomerCity:
    'C.POS:' value = /.{1,25}/
;

Refund:
    'Credito ref. factura:'
    refund_serial_number = /\w{1,4}/ '/'
    manager_serial_number = /\w{1,3}/ '/'
    document_number = /\w{1,8}/
;

CheckSignature:
    value = /.{1,6}/ Skip
;

EscPosCommand:
    Skip
;

/* ************** Support types **************** */

ReceiptIdRegEx:
    /[a-zA-Z0-9-@\/\.\#]+/
;

DateRegEx:
    /\d{1,4}\W?\d{1,4}\W?\d{1,4}/
;

TimeRegEx:
    /\d{1,2}\W?\d{1,2}(\W?\d{1,2})?(\s)?(am|pm|AM|PM)?/
;

IntegerRegEx:
    /-?\d+([\,\.]\d{3})*/
;

NumberRegEx:
    /-?(\d+([\,\.]\d{3})*|(\d+))[\.\,]\d{2}/
;

CurrencyRegEx:
    /([A-Z]{1,3}|\$|\€)?\s?\-?(\d{1,3}([\,\.]\d{3})+|(\d+))([\.\,]\d{1,2})?\s?([A-Z]{1,3}|\$|\€)?/
;

TillRef:
    /[a-zA-Z0-9]+([\W]?[a-zA-Z0-9]+){1,2}/
;

Cashier:
    /[a-zA-Z]{2,}\s?[a-zA-z0-9]{1,}\'?\-?\.?\s?([a-zA-Z]{2,})?\s?([a-zA-Z]{1,})?/
;

PercentageRegEx:
    /\-?\s?\%?\s?\d{1,3}([.\,]?\d{1,2})?\s?\%?/
;

MeansRegEx:
	/[a-zA-Z]{2,}\s?[a-zA-z0-9]{1,}\'?\-?\.?\s?([a-zA-Z]{2,})?\s?([a-zA-Z]{1,})?/
;

SeparatorLineRegEx:
	/[-=\*\.]*/
;

Skip:
    /(?u).*$/
;
